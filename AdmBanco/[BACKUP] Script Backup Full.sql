/***********************************************************************************
 * Autor: Diego Dantas                                                             *
 * Data: 12/03/2013                                                                *
 * Objetivo: Realizar backup full de todos os bancos e caso o mesmo não tenha sua  *
 *           pasta , a mesma será criada. Caso o arquivo de Backup exista o mesmo  *
 *           será sob escrito.                                                     *
 **********************************************************************************/ 
--DECLARAÇÃO DAS VARIAVEIS QUE SERÃO UTILIZADAS 
DECLARE @NAME VARCHAR(150) -- DATABASE NAME
DECLARE @PATH VARCHAR(256) -- PATH FOR BACKUP FILES
DECLARE @FILENAME VARCHAR(800) -- FILENAME FOR BACKUP
DECLARE @DIRETORIO VARCHAR(500) -- CRIA SUBDIRETORIO COM NOME DO BANCO CASO NÃO TENHA
DECLARE @BACKUPSETID AS INT

-- SETA O CAMINHO DA PASTA QUE QUERO REALIZAR OS BACKUPS
SET @PATH = 'D:\BACKUP\COMPLETO\'

-- CURSOR QUE BUSCA O NOME DO BANCO QUE É PARA REALIZAR O BACKUP
DECLARE BD_CURSOR CURSOR FOR
SELECT DAT.NAME
FROM MASTER.DBO.SYSDATABASES DAT
     INNER JOIN SYS.DATABASES DAT1 ON DAT.DBID = DAT1.DATABASE_ID AND DAT1.STATE = 0
WHERE DAT.NAME NOT IN ('MODEL','TEMPDB') -- CONDIÇÃO COM BANCOS QUE NÃO É PARA REALIZAR BACKUP.

-- ABRE O CURSOR
OPEN BD_CURSOR 

-- LÊ O CURSOR E ME TRAZ O NOME DO BANCO DE DADOS
FETCH NEXT FROM BD_CURSOR INTO @NAME 

-- REPETIÇÃO DE ATÉ QUANDO QUE SEJA FIM
WHILE @@FETCH_STATUS = 0 
BEGIN 

-- SETA O CAMINHO DO DIRETORIO MAPEADO NA VARIAVEL @PATH + O NOME DO BANCO
       SET @DIRETORIO = @PATH + @NAME

-- CRIA UM PASTA NO CAMINHO SETADO ACIMA EM @DIRETORIO "D:\BACKUP\COMPLETO\" COM O NOME DO BANCO
       EXECUTE MASTER.DBO.XP_CREATE_SUBDIR @DIRETORIO

-- SETA A EXTENSÃO _BACKUP.BAK AO BACKUP QUE SERÁ GERADO
       SET @FILENAME = @PATH + @NAME + '\' + @NAME +'_BACKUP.BAK'

-- INSTRUÇÃO QUE REALIZA O BACKUP DO BANCO
       BACKUP DATABASE @NAME TO DISK = @FILENAME WITH NOFORMAT, INIT, SKIP, NOREWIND, NOUNLOAD, COMPRESSION,  STATS = 10

-- BUSCA INFORMAÇÕES DA REALIZAÇÃO DE BACKUP NO BANCO MSDB PARA PODER CHECAR SE O BACKUP ESTA INTEGRO
       SELECT @BACKUPSETID = POSITION
          FROM MSDB..BACKUPSET
       WHERE DATABASE_NAME=@NAME AND BACKUP_SET_ID=(SELECT MAX(BACKUP_SET_ID) FROM MSDB..BACKUPSET WHERE DATABASE_NAME=@NAME )
       IF @BACKUPSETID IS NULL BEGIN RAISERROR(N'VERIFY FAILED. BACKUP INFORMATION FOR DATABASE NOT FOUND.', @name , 16, 1) END
       RESTORE VERIFYONLY FROM  DISK = @filename WITH  FILE = @BACKUPSETID,  NOUNLOAD,  NOREWIND

-- LÊ O PROXIMO REGISTRO  
       FETCH NEXT FROM BD_CURSOR INTO @NAME 
END 

-- FECHA O CURSOR
CLOSE BD_CURSOR 

-- DESALOCA O CURSOR
DEALLOCATE BD_CURSOR;
